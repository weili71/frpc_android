apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdkVersion 35
    namespace "com.car.frpc_android"

    defaultConfig {
        applicationId "com.car.frpc_android"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 5
        versionName "0.61.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    tasks.withType(JavaCompile).configureEach{
        options.fork = true
        options.forkOptions.jvmArgs +=[
                '--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
                '--add-exports=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED',
                '--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED']
    }

    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }

    // Define build flavors for x86 and arm combinations
    flavorDimensions "abi"
    productFlavors {
        x86 {
            dimension "abi"
            ndk {
                abiFilters "x86", "x86_64" // Bundle x86 and x86_64
            }
        }
        arm {
            dimension "abi"
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a" // Bundle armeabi-v7a and arm64-v8a
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def flavor = variant.productFlavors[0].name // Get the flavor name (x86 or arm)
            outputFileName = "frpc_android-${variant.versionName}-${flavor}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
//    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.navigation:navigation-fragment:2.8.3'
    implementation 'androidx.navigation:navigation-ui:2.8.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    implementation(name: 'frpclib-sources', ext: 'jar')
    implementation(name: 'frpclib', ext: 'aar')

    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'

    implementation 'com.github.ahmadaghazadeh:CodeEditor:1.0.17'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'org.jetbrains:annotations:23.0.0'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4'
    implementation 'io.github.jeremyliao:live-event-bus-x:1.8.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation "androidx.multidex:multidex:2.0.1"

    def room_version = "2.6.1"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"


}
